;mlevph
;avance-version (10/01/28)
;homonuclear Hartman-Hahn transfer using MLEV17 sequence
;   for mixing
;using two power levels for excitation and spinlock
;phase sensitive
;
;A. Bax & D.G. Davis, J. Magn. Reson. 65, 355-360 (1985)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=


#include <Avance.incl>
#include <Delay.incl>
#   ifdef POL
#include <Polariser.incl>
#   else
#   endif 

"acqt0=-p1*2/3.1416"
define delay Time_IJP
"Time_IJP=d40"
define delay Time_BH2
"Time_BH2=d41"
define delay Time_EJP
"Time_EJP=d42"
define delay Time_IJS
"Time_IJS=d43"
define delay Time_EJS
"Time_EJS=d44"
define delay RD_time
"RD_time=d45-(d11+d1+d40+d41+d42+aq)"
define delay pol_field
"pol_field = abs(cnst31)"


"p5=p6*.667"
"p7=p6*2"
"d12=20u"


"in0=inf1"

"d0=in0/2-p1*2/3.1416-4u"


"SCALEF=p7*2/p5"
"FACTOR1=((d9-p17*2)/(p6*64+p5))/SCALEF"
"l1=FACTOR1*SCALEF"


1 ze

#   ifdef POL
	subr IPSO_CMD()						;force to ipso mode
	F__IN_MIX_CHAMBER 				;Sample is in the polariser	
#   else
#   endif 

2 d1

#   ifdef POL
	RD_time										;recovery time
  subr BUBBLE_H2() 					; bubble H2 para in the sample.
  Time_BH2									; duration of the bubble
  subr STOP_EVEN()					; stop of bubble

  subr INJECT_PROBE() 			; transfer sample from polariser to probe.
  Time_IJP									; delay for loading sample from the polariser to the flowcell.
#   else
#   endif 

3 d12 pl1:f1
  p1 ph1
  d0
  4u pl10:f1
  (p17 ph26)
						;begin MLEV17
4 (p6 ph22 p7 ph23 p6 ph22)
  (p6 ph24 p7 ph25 p6 ph24)
  (p6 ph24 p7 ph25 p6 ph24)
  (p6 ph22 p7 ph23 p6 ph22)
  (p6 ph24 p7 ph25 p6 ph24)
  (p6 ph24 p7 ph25 p6 ph24)
  (p6 ph22 p7 ph23 p6 ph22)
  (p6 ph22 p7 ph23 p6 ph22)
  (p6 ph24 p7 ph25 p6 ph24)
  (p6 ph22 p7 ph23 p6 ph22)
  (p6 ph22 p7 ph23 p6 ph22)
  (p6 ph24 p7 ph25 p6 ph24)
  (p6 ph22 p7 ph23 p6 ph22)
  (p6 ph22 p7 ph23 p6 ph22)
  (p6 ph24 p7 ph25 p6 ph24)
  (p6 ph24 p7 ph25 p6 ph24)
  (p5 ph23)
  lo to 4 times l1 
						;end MLEV17
  (p17 ph26) 
  go=2 ph31

#   ifdef POL  
  subr EJECT_PROBE() 				; transfer sample from probe to polariser.
  Time_EJP									; delay for the return sample from the flowcell to the polarizer.
#   else
#   endif 

  d1 mc #0 to 2 F1PH(calph(ph1, +90), caldel(d0, +in0))
exit


ph1=0 2 2 0 1 3 3 1
ph22=3 1 3 1 0 2 0 2
ph23=0 2 0 2 1 3 1 3
ph24=1 3 1 3 2 0 2 0
ph25=2 0 2 0 3 1 3 1
ph26=0 2 0 2 1 3 1 3
ph31=0 2 2 0 1 3 3 1


;pl1 : f1 channel - power level for pulse (default)
;pl10: f1 channel - power level for TOCSY-spinlock
;p1 : f1 channel -  90 degree high power pulse
;p5 : f1 channel -  60 degree low power pulse
;p6 : f1 channel -  90 degree low power pulse
;p7 : f1 channel - 180 degree low power pulse
;p17: f1 channel - trim pulse                        [2.5 msec]
;d0 : incremented delay (2D)
;d9 : TOCSY mixing time
;d12: delay for power switching                      [20 usec]
;l1: loop for MLEV cycle: (((p6*64) + p5) * l1) + (p17*2) = mixing time
;inf1: 1/SW = 2 * DW
;in0: 1/(1 * SW) = 2 * DW
;nd0: 1
;NS: 8 * n
;DS: 16
;td1: number of experiments
;FnMODE: States-TPPI, TPPI, States or QSEQ
;d1 : relaxation delay; set to very short for hyperpolarisation experiments 
;d11: delay for disk I/O                                    [30 msec]
;ns : number of scans: set to = 1 for hyperpolarisation experiments
;d40: delay for loading sample from the polariser to the flowcell
;d41: duration of the bubbling of parahydrogen 
;d42: delay for returning sample from the flowcell to the polarizer
;d43: delay for loading sample from the vial to the polarizer
;d44: delay for emptying sample from the polarizer to  the vial
;d45: delay betweem scans
;l0 : accumulate L0 scans; re-polarising between scans
;ns : 1
;cnst31: polarization magnetic field





;Processing

;PHC0(F1): 180
;PHC1(F1): -180
;FCOR(F1): 1



;$Id: mlevph,v 1.10.2.1 2010/02/02 15:31:39 ber Exp $
